Rem BasicCard Sample Source Code
Rem ------------------------------------------------------------------
Rem Copyright (C) 1997-1999 ZeitControl GmbH
Rem You have a royalty-free right to use, modify, reproduce and 
Rem distribute the Sample Application Files (and/or any modified 
Rem version) in any way you find useful, provided that you agree 
Rem that ZeitControl GmbH has no warranty, obligations or liability
Rem for any Sample Application Files.
Rem ------------------------------------------------------------------

Rem  CARDUTIL.DEF
Rem
Rem  Some utility procedures for handling card insertion etc.

#IfNotDef CardutilDefIncluded ' Prevent multiple inclusion
Const CardutilDefIncluded = True

#Include COMMERR.DEF
#Include ERRFILE.DEF
#Include ..\Lib\MISC.DEF ' Load library MISC.LIB for procedure Sleep()

Rem  Sub WaitForCard()
Rem
Rem  Check the card reader, and prompt for a card if it's empty

Sub WaitForCard()

  Rem  Check that the card reader is responding
  CardReader
  If SW1SW2 = swNoCardReader Then
    Private Filenum : Filenum = ErrorFilenum()
    Print #Filenum, "No card reader detected"
    If Filenum Then Close Filenum
    Exit
  End If

  Call CheckSW1SW2()

  Rem  Wait for a card to become available for use
  CardInReader : If SW1SW2 = swCommandOK Then Exit Sub
  If (SW1SW2 <> swNoCardInReader) AND (SW1SW2 <> swPcscReaderBusy) Then Call CheckSW1SW2()
  Print "Insert card in reader, or press any key to abort:" ;
  Do
    If InKey$ <> "" Then Exit
    CardInReader : If SW1SW2 = swCommandOK Then Print : Exit Sub
    If (SW1SW2 <> swNoCardInReader) AND (SW1SW2 <> swPcscReaderBusy) Then Call CheckSW1SW2()
    Call Sleep(50) ' Let other processes use the CPU
  Loop

End Sub ' WaitForCard

Rem  Sub WaitForNoCard()
Rem
Rem  Wait for the card to be removed from the reader

Sub WaitForNoCard()

  CardInReader : If SW1SW2 = swNoCardInReader Then Exit Sub
  if SW1SW2 <> swPcscReaderBusy then Call CheckSW1SW2()
  Print "Remove card from reader, or press any key to abort:" ;
  Do
    If InKey$ <> "" Then Exit
    CardInReader : If SW1SW2 = swNoCardInReader Then Print : Exit Sub
    if SW1SW2 <> swPcscReaderBusy then Call CheckSW1SW2()
    Call Sleep(50) ' Let other processes use the CPU
  Loop

End Sub ' WaitForNoCard

#EndIf ' CardutilDefIncluded
